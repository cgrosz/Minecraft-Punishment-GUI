#
# Staff Tool - Punishment GUI - Created by LuuMiz
#
# Required: Skript, SKQuery
#           Essentials, AdvancedBan
#
# Allows for staff members to easily mute, jail, temp ban, and ban players.
# Allows admins to choose to annouce bans to chat for all players to see.
# Shows punishment history for an individual player which includes reason, date, staff member who punished.
# Logs ALL punishments to a server file for safe keeping.
# Change permissions to the correct staff ranks
#   Admins should only be able to public ban
#   Helpers should only have access to mute commands
#   Etc.
#

# #######################################################################################################
#                           MAIN GUI WINDOW                                                             #
# #######################################################################################################

command /adminpunish <offlinePlayer> [<boolean>]:
    permission: minecraft.op    #helper+
    aliases:/aap
    trigger:
        wait 3 ticks
        open chest with 1 row named "&4&l&nPlayer Punishment" to player
        wait 1 tick
        set {_aapslot} to 0
        loop 9 times:
            format slot {_aapslot} of player with gray stained glass pane named "&6" to be unstealable
            add 1 to {_aapslot}
        
        set {punishment_gui_%player%} to true

        if arg-2 is not set:
            format slot 0 of player with glowing red wool named "&4Public Punishment" to close then run [execute player command "/adminpublictrue %arg 1%"]
            format slot 2 of player with glowing conduit named "&eMute %arg 1%" to close then run [execute player command "/adminmute %arg 1%"]
            format slot 4 of player with glowing conduit named "&6Jail %arg 1%" to close then run [execute player command "/adminjail %arg 1%"]
            format slot 6 of player with glowing conduit named "&cBan %arg 1%" to close then run [execute player command "/adminban %arg 1%"]
            format slot 8 of player with glowing player head named "&7%arg 1% Punishment History" to close then run [execute player command "/adminphistory %arg 1%"]
        else if arg-2 is false:
            format slot 0 of player with red wool named "&4Public Punishment" to close then run [execute player command "/adminpublictrue %arg 1%"]
            format slot 2 of player with conduit named "&eMute %arg 1%" to close then run [execute player command "/adminmute %arg 1%"]
            format slot 4 of player with conduit named "&6Jail %arg 1%" to close then run [execute player command "/adminjail %arg 1%"]
            format slot 6 of player with conduit named "&cBan %arg 1%" to close then run [execute player command "/adminban %arg 1%"]
            format slot 8 of player with player head named "&7%arg 1% Punishment History" to close then run [execute player command "/adminphistory %arg 1%"]
        else if arg-2 is true:
            format slot 0 of player with green wool named "&2Public Punishment" to close then run [execute player command "/adminpublicfalse %arg 1%"]
            format slot 2 of player with conduit named "&eMute %arg 1%" to close then run [execute player command "/adminmute %arg 1% true"]
            format slot 4 of player with conduit named "&6Jail %arg 1%" to close then run [execute player command "/adminjail %arg 1% true"]
            format slot 6 of player with conduit named "&cBan %arg 1%" to close then run [execute player command "/adminban %arg 1% true"]
            format slot 8 of player with player head named "&7%arg 1% Punishment History" to close then run [execute player command "/adminphistory %arg 1%"]


# #######################################################################################################
#                           Start of Mute, Jail, Ban GUI Windows                                        #
# #######################################################################################################
command /adminpublictrue <offlinePlayer>:
    permission: minecraft.op    #admin+
    trigger:
        wait 3 ticks
        set {punishment_gui_%player%} to true
        execute player command "/adminpunish %arg 1% true"

command /adminpublicfalse <offlinePlayer>:
    permission: minecraft.op    #admin+
    trigger:
        wait 3 ticks
        set {punishment_gui_%player%} to true
        execute player command "/adminpunish %arg 1% false"

command /adminannounce <offlinePlayer> <text>:
    permission: minecraft.op    #admin+
    trigger:
        set {punishment_gui_%player%} to true
        broadcast "&c&l%arg 1% was %arg 2%"

command /adminmute <offlinePlayer> [<boolean>]:
    permission: minecraft.op    #helper+
    trigger:        
        #need to wait here for close inv animation to finish from previous gui
        wait 3 ticks

        set {punishment_gui_%player%} to true

        open chest with 1 row named "&4&l&nReason to Mute" to player
        wait 1 tick
        set {_muteslot} to 0
        loop 9 times:
            format slot {_muteslot} of player with gray stained glass pane named "&6" to be unstealable
            add 1 to {_muteslot}
        if arg-2 is true:
            format slot 2 of player with snowball named "&eNewbie Troll" to close then run [close player's inventory]->[execute player command "/tempwarn %arg 1% 15m Newbie Troll"]->[execute player command "/adminannounce %arg 1% muted for Newbie Trolls"]->[execute player command "/adminaddmute %arg 1% Newbie Troll"]
            format slot 4 of player with snowball named "&eRacism/Hate Speech" to close then run [close player's inventory]->[execute player command "/tempwarn %arg 1% 1h Hate Speech"]->[execute player command "/adminannounce %arg 1% muted for Racism/Hate Speech"]->[execute player command "/adminaddmute %arg 1% Hate Speech"]
            format slot 6 of player with snowball named "&eArguing Too Much" to close then run [close player's inventory]->[execute player command "/tempwarn %arg 1% 10m Arguing"]->[execute player command "/adminannounce %arg 1% muted for Arguing"]->[execute player command "/adminaddmute %arg 1% Arguing"]
            format slot 8 of player with barrier named "&4Back" to close then run [close player's inventory]->[execute player command "/adminpunish %arg 1%"]
        else:
            format slot 2 of player with snowball named "&eNewbie Troll" to close then run [close player's inventory]->[execute player command "/tempwarn %arg 1% 15m Newbie Troll"]->[execute player command "/adminaddmute %arg 1% Newbie Troll"]
            format slot 4 of player with snowball named "&eRacism/Hate Speech" to close then run [close player's inventory]->[execute player command "/tempwarn %arg 1% 1h Hate Speech"]->[execute player command "/adminaddmute %arg 1% Hate Speech"]
            format slot 6 of player with snowball named "&eArguing Too Much" to close then run [close player's inventory]->[execute player command "/tempwarn %arg 1% 10m Arguing"]->[execute player command "/adminaddmute %arg 1% Arguing"]
            format slot 8 of player with barrier named "&4Back" to close then run [close player's inventory]->[execute player command "/adminpunish %arg 1%"]

command /adminjail <offlinePlayer> [<boolean>]:
    permission: minecraft.op    #mod+
    trigger:        
        #need to wait here for close inv animation to finish from previous gui
        wait 3 ticks

        set {punishment_gui_%player%} to true

        open chest with 1 row named "&4&l&nReason to Jail" to player
        wait 1 tick
        set {_jailslot} to 0
        loop 9 times:
            format slot {_jailslot} of player with gray stained glass pane named "&6" to be unstealable
            add 1 to {_jailslot}
        if arg-2 is true:
            format slot 2 of player with snowball named "&6Newbie Troll" to close then run [close player's inventory]->[execute player command "/jail %arg 1% jail 15m"]->[execute player command "/adminannounce %arg 1% jalied for Newbie Trolls"]->[execute player command "/adminaddjail %arg 1% Newbie Troll"]
            format slot 4 of player with snowball named "&6Non-Newbie Hacks" to close then run [close player's inventory]->[execute player command "/jail %arg 1% jail 3h"]->[execute player command "/adminannounce %arg 1% jailed for Non-Newbie Hacks"]->[execute player command "/adminaddjail %arg 1% Hacks"]
            format slot 6 of player with snowball named "&6Other Behavior (10m)" to close then run [close player's inventory]->[execute player command "/jail %arg 1% jail 10m"]->[execute player command "/adminannounce %arg 1% jailed for Other Behavior"]->[execute player command "/adminaddjail %arg 1% Other"]
            format slot 8 of player with barrier named "&4Back" to close then run [close player's inventory]->[execute player command "/adminpunish %arg 1%"]
        else:
            format slot 2 of player with snowball named "&6Newbie Troll" to close then run [close player's inventory]->[execute player command "/jail %arg 1% jail 15m"]->[execute player command "/adminaddjail %arg 1% Newbie Troll"]
            format slot 4 of player with snowball named "&6Non-Newbie Hacks" to close then run [close player's inventory]->[execute player command "/jail %arg 1% jail 3h"]->[execute player command "/adminaddjail %arg 1% Hacks"]
            format slot 6 of player with snowball named "&6Other Behavior (10m)" to close then run [close player's inventory]->[execute player command "/jail %arg 1% jail 10m"]->[execute player command "/adminaddjail %arg 1% Other"]
            format slot 8 of player with barrier named "&4Back" to close then run [close player's inventory]->[execute player command "/adminpunish %arg 1%"]

command /adminban <offlinePlayer> [<boolean>]:
    permission: minecraft.op    #mod+
    trigger:        
        #need to wait here for close inv animation to finish from previous gui
        wait 3 ticks

        set {punishment_gui_%player%} to true

        open chest with 1 row named "&4&l&nReason to Ban" to player
        wait 1 tick
        set {_muteslot} to 0
        loop 9 times:
            format slot {_muteslot} of player with gray stained glass pane named "&6" to be unstealable
            add 1 to {_muteslot}
        if arg-2 is true:
            format slot 2 of player with snowball named "&cNewbie Troll" to close then run [close player's inventory]->[execute player command "/ban %arg 1% Newbie Troll"]->[execute player command "/adminannounce %arg 1% banned for Newbie Trolls"]->[execute player command "/adminaddban %arg 1% Newbie Troll"]
            format slot 4 of player with snowball named "&cHacks" to close then run [close player's inventory]->[execute player command "/ban %arg 1% Hacks"]->[execute player command "/adminannounce %arg 1% banned for Hacks"]->[execute player command "/adminaddban %arg 1% Hacks"]
            format slot 6 of player with snowball named "&cOther Behavior" to close then run [close player's inventory]->[execute player command "/ban %arg 1% Other"]->[execute player command "/adminannounce %arg 1% banned for Other Behavior"]->[execute player command "/adminaddban %arg 1% Other"]
            format slot 8 of player with barrier named "&4Back" to close then run [close player's inventory]->[execute player command "/adminpunish %arg 1%"]
        else:
            format slot 2 of player with snowball named "&cNewbie Troll" to close then run [close player's inventory]->[execute player command "/ban %arg 1% Newbie Troll"]->[execute player command "/adminaddban %arg 1% Newbie Troll"]
            format slot 4 of player with snowball named "&cHacks" to close then run [close player's inventory]->[execute player command "/ban %arg 1% Hacks"]->[execute player command "/adminaddban %arg 1% Hacks"]
            format slot 6 of player with snowball named "&cOther Behavior" to close then run [close player's inventory]->[execute player command "/ban %arg 1% Other"]->[execute player command "/adminaddban %arg 1% Other"]
            format slot 8 of player with barrier named "&4Back" to close then run [close player's inventory]->[execute player command "/adminpunish %arg 1%"]

# #######################################################################################################
#                           PLAYER PUNISHMENT HISTORY GUI WINDOW                                        #
# #######################################################################################################
command /adminphistory <offlinePlayer> [<boolean>]:
    permission: minecraft.op    #mod+
    trigger:        
        #need to wait here for close inv animation to finish from previous gui
        wait 3 ticks

        set {punishment_gui_%player%} to true

        open chest with 4 rows named "&4&l&nPlayer Punishment History" to player
        wait 1 tick
        set {_historyslot} to 0
        loop 36 times:
            format slot {_historyslot} of player with gray stained glass pane named "&6" to be unstealable
            add 1 to {_historyslot}
        
        format slot 0 of player with snowball named "&eMutes" with lore "&e%{countMutes_%arg 1%}%" to be unstealable
        format slot 9 of player with snowball named "&6Jails" with lore "&6%{countJails_%arg 1%}%" to be unstealable
        format slot 18 of player with snowball named "&cBans" with lore "&c%{countBans_%arg 1%}%" to be unstealable
        format slot 31 of player with barrier named "&4Back" to close then run [close player's inventory]->[execute player command "/adminpunish %arg 1%"]

        # 8 most recent mutes with reason and date and staff who banned
        set {_count1} to size of {mutes_%arg 1%::*}
        set {_index1} to 1
        loop {mutes_%arg 1%::*}:
            format slot {_index1} of player with yellow wool named "&e%{mutes_%arg 1%::%{_count1}%}%" with lore "&e%{mutesDate_%arg 1%::%{_count1}%}%" and "&a%{mutesBy_%arg 1%::%{_count1}%}%" to be unstealable
            remove 1 from {_count1}
            add 1 to {_index1}
            if {_index1} > 8:
                stop

        # 8 most recent jails with reason and date and staff who banned
        set {_count2} to size of {jails_%arg 1%::*}
        set {_index2} to 10
        loop {jails_%arg 1%::*}:
            format slot {_index2} of player with orange wool named "&6%{jails_%arg 1%::%{_count2}%}%" with lore "&6%{jailsDate_%arg 1%::%{_count2}%}%" and "&a%{jailsBy_%arg 1%::%{_count2}%}%" to be unstealable
            remove 1 from {_count2}
            add 1 to {_index2}
            if {_index2} > 17:
                stop

        # 8 most recent bans with reason and date and staff who banned
        set {_count3} to size of {bans_%arg 1%::*}
        set {_index3} to 19
        loop {bans_%arg 1%::*}:
            format slot {_index3} of player with red wool named "&c%{bans_%arg 1%::%{_count3}%}%" with lore "&c%{bansDate_%arg 1%::%{_count3}%}%" and "&a%{bansBy_%arg 1%::%{_count3}%}%" to be unstealable
            remove 1 from {_count3}
            add 1 to {_index3}
            if {_index3} > 26:
                stop

command /adminaddmute <offlinePlayer> <text>:
    permission: minecraft.op    #helper+
    trigger:
        if {countMutes_%arg 1%} is not set:
            set {countMutes_%arg 1%} to 1
        else:
            add 1 to {countMutes_%arg 1%}
        add arg-2 to {mutes_%arg 1%::*}
        add now to {mutesDate_%arg 1%::*}
        add player to {mutesBy_%arg 1%::*}
        log "MUTE : %arg 1% muted by %player% for %arg 2%" to "LOGFOLDER/punishments.log"

command /adminaddjail <offlinePlayer> <text>:
    permission: minecraft.op    #mod+
    trigger:
        if {countJails_%arg 1%} is not set:
            set {countJails_%arg 1%} to 1
        else:
            add 1 to {countJails_%arg 1%}
        add arg-2 to {jails_%arg 1%::*}
        add now to {jailsDate_%arg 1%::*}
        add player to {jailsBy_%arg 1%::*}
        log "JAIL : %arg 1% jailed by %player% for %arg 2%" to "LOGFOLDER/punishments.log"

command /adminaddban <offlinePlayer> <text>:
    permission: minecraft.op    #mod+
    trigger:
        if {countBans_%arg 1%} is not set:
            set {countBans_%arg 1%} to 1
        else:
            add 1 to {countBans_%arg 1%}
        add arg-2 to {bans_%arg 1%::*}
        add now to {bansDate_%arg 1%::*}
        add player to {bansBy_%arg 1%::*}
        log "BAN : %arg 1% banned by %player% for %arg 2%" to "LOGFOLDER/punishments.log"

# #######################################################################################################
#                           Handles player's inventory while inside the GUI                             #
# #######################################################################################################
on inventory click:
    if {punishment_gui_%player%} is true:
        cancel event

on inventory close:
    set {punishment_gui_%player%} to false

